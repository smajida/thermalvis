cmake_minimum_required(VERSION 2.8 FATAL_ERROR)

project(THERMALVIS)

SET(THERMALVIS_COMPILE_FLAGS "-D_THERMALVIS_")
SET(ADDITIONAL_INCLUDES "")
SET(ADDITIONAL_LIBRARIES "")

OPTION(BUILD_APPS "Build demo apps." TRUE)
OPTION(BUILD_FOR_ROS "Build ROS interfaces and functions." FALSE) # If you can search for presence of ROS, you can make this conditional..

IF( (${CMAKE_GENERATOR} STREQUAL "Visual Studio 10") OR (${CMAKE_GENERATOR} STREQUAL "Visual Studio 10 Win64") )
	SET(msvc_ABBREVIATION "msvc2010")
	SET(MSVC_ABBREVIATION_CAPS "MSVC2010")
ELSEIF( (${CMAKE_GENERATOR} STREQUAL "Visual Studio 11") OR (${CMAKE_GENERATOR} STREQUAL "Visual Studio 11 Win64") )
	SET(msvc_ABBREVIATION "msvc2012")
	SET(MSVC_ABBREVIATION_CAPS "MSVC2012")
ELSEIF( (${CMAKE_GENERATOR} STREQUAL "Visual Studio 12") OR (${CMAKE_GENERATOR} STREQUAL "Visual Studio 12 Win64") )
	SET(msvc_ABBREVIATION "msvc2013")
	SET(MSVC_ABBREVIATION_CAPS "MSVC2013")
ENDIF()

IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
	SET(ARCH_ABBREV "64")
	SET(ARCH_BITS "64")
ELSE()
	SET(ARCH_ABBREV "x86")
	SET(ARCH_BITS "32")
ENDIF()
		
#set(CMAKE_AUTOMOC ON)
#set(CMAKE_INCLUDE_CURRENT_DIR ON)

SET(Qt5Widgets_DIR "C:/Qt/Qt5.3.1/5.3/msvc2012_opengl/lib/cmake/Qt5Widgets" CACHE STRING "Default search path for QtWidgets cmake file")
find_package(Qt5Widgets QUIET)

IF(Qt5Widgets_FOUND)
	IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
		OPTION(BUILD_GUI "Build qt-based GUI controls." FALSE)
	ELSE()
		OPTION(BUILD_GUI "Build qt-based GUI controls." TRUE)
	ENDIF()
ELSE()
	OPTION(BUILD_GUI "Build qt-based GUI controls." FALSE)
ENDIF()

IF(BUILD_GUI)
	IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
		MESSAGE(FATAL_ERROR "Qt libraries do not yet work in 64-bit mode. Please switch to 32-bit mode for this functionality, or disable the <BUILD_GUI> option.")
	ENDIF()
	find_package(Qt5Widgets)
ENDIF()

IF(BUILD_GUI AND Qt5Widgets_FOUND)
	ADD_DEFINITIONS( -D_USE_QT_ )
	string(REPLACE "." ";" Qt5Widgets_VERSION_SEP ${Qt5Widgets_VERSION})
	list(GET Qt5Widgets_VERSION_SEP 0 QT_VERSION_MAJOR)
	list(GET Qt5Widgets_VERSION_SEP 1 QT_VERSION_MINOR)
	if(POLICY CMP0020)
		cmake_policy(SET CMP0020 NEW)
	endif()
	SET(Qt_STREAMER_BUILD_DIR "build-streamer-Desktop_Qt_${QT_VERSION_MAJOR}_${QT_VERSION_MINOR}_${MSVC_ABBREVIATION_CAPS}_openGL_${ARCH_BITS}bit-Debug")
	SET(QTSTREAMER_BUILD_PATH "${CMAKE_CURRENT_SOURCE_DIR}/qt/${Qt_STREAMER_BUILD_DIR}" CACHE STRING "Build directory for Qt streamer app")
	SET(Qt_FLOW_BUILD_DIR "build-flow-Desktop_Qt_${QT_VERSION_MAJOR}_${QT_VERSION_MINOR}_${MSVC_ABBREVIATION_CAPS}_openGL_${ARCH_BITS}bit-Debug")
	SET(QTFLOW_BUILD_PATH "${CMAKE_CURRENT_SOURCE_DIR}/qt/${Qt_FLOW_BUILD_DIR}" CACHE STRING "Build directory for Qt flow app")
	SET(Qt_SLAM_BUILD_DIR "build-slam-Desktop_Qt_${QT_VERSION_MAJOR}_${QT_VERSION_MINOR}_${MSVC_ABBREVIATION_CAPS}_openGL_${ARCH_BITS}bit-Debug")
	SET(QTSLAM_BUILD_PATH "${CMAKE_CURRENT_SOURCE_DIR}/qt/${Qt_SLAM_BUILD_DIR}" CACHE STRING "Build directory for Qt slam app")
	#LIST(APPEND ADDITIONAL_LIBRARIES Qt5::WinMain)
ELSEIF(BUILD_GUI)
	MESSAGE(FATAL_ERROR "Qt5 was not found. Please check that the <Qt5Widgets_DIR> variable points to the location of <Qt5WidgetsConfig.cmake>. If you want to build without the GUI, please deselect the option <BUILD_GUI>")
ENDIF()

IF(WIN32 OR win64)
	IF (BUILD_FOR_ROS)
		MESSAGE(FATAL_ERROR "ROS-compatibility is not possible in Windows environment. Please deselect <BUILD_FOR_ROS>.")
	ENDIF()
ENDIF()

string(REGEX REPLACE "\\\\" "/" USERPROFILE "$ENV{USERPROFILE}")
MESSAGE(STATUS "USERPROFILE = ${USERPROFILE}")

SET(DEFAULT_SAMPLE_DATA "${USERPROFILE}/Documents/Data/optris/test_seq_001" CACHE STRING "...")
ADD_DEFINITIONS(-D_DEFAULT_SAMPLE_DATA_="${DEFAULT_SAMPLE_DATA}")

IF(EXISTS "C:/eigen")
	OPTION(USE_EIGEN "Build Eigen-dependent functions." TRUE)
ELSE()
	OPTION(USE_EIGEN "Build Eigen-dependent functions." FALSE)
ENDIF()

IF(USE_EIGEN)
	IF(WIN32 OR win64)
		SET(Eigen_DIR "C:/eigen" CACHE STRING "..." FORCE)
	ELSE()
		SET(Eigen_DIR "" CACHE STRING "..." FORCE)
	ENDIF()
	
	IF(NOT EXISTS "${Eigen_DIR}")
		MESSAGE(FATAL_ERROR "Provided path for Eigen directory does not appear to exist!")
	ENDIF()
	IF(NOT EXISTS "${Eigen_DIR}/cmake/FindEigen3.cmake")
		MESSAGE(FATAL_ERROR "Could not find <${Eigen_DIR}/cmake/FindEigen3.cmake>!")
	ENDIF()
	MESSAGE(STATUS "Adding following path to CMAKE_MODULE_PATH: <${Eigen_DIR}/cmake>")
	SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${Eigen_DIR}/cmake")
	FIND_PACKAGE(Eigen3)
	IF(EIGEN3_FOUND)
		ADD_DEFINITIONS( -D_USE_EIGEN_ )
		SET(ADDITIONAL_INCLUDES ${ADDITIONAL_INCLUDES} ${EIGEN3_INCLUDE_DIR})
	ELSE()
		MESSAGE(FATAL_ERROR "Eigen not found! Much of the libraries can be built without Eigen, however, so consider opting not to use it.")
	ENDIF()
ENDIF()

ADD_DEFINITIONS( -D_CRT_SECURE_NO_WARNINGS )

find_package(PCL QUIET)
IF(PCL_FOUND)
	IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
		STRING( FIND ${PCL_COMMON_LIBRARY} "32" APOSITION )
		IF (NOT "${APOSITION}" STREQUAL "-1")
			OPTION(USE_PCL "Build PCL-dependent functions." FALSE)
			MESSAGE(WARNING "This is a 64-bit build, and it looks like CMake actually found the 32-bit PCL libraries to link to! Therefore PCL has been excluded from the build.")
		ELSE()
			OPTION(USE_PCL "Build PCL-dependent functions." TRUE)
		ENDIF()
	ELSE()
		STRING( FIND ${PCL_COMMON_LIBRARY} "64" APOSITION ) 
		IF (NOT "${APOSITION}" STREQUAL "-1")
			OPTION(USE_PCL "Build PCL-dependent functions." FALSE)
			MESSAGE(WARNING "This is a 32-bit build, and it looks like CMake actually found the 64-bit PCL libraries to link to! Therefore PCL has been excluded from the build.")
		ELSE()
			OPTION(USE_PCL "Build PCL-dependent functions." TRUE)
		ENDIF()
	ENDIF()
ELSE()
	OPTION(USE_PCL "Build PCL-dependent functions." FALSE)
ENDIF()

IF(USE_PCL)
	FIND_PACKAGE( PCL COMPONENTS common io )
	IF(PCL_FOUND)
		MESSAGE(STATUS "PCL Found!")
		ADD_DEFINITIONS( -D_USE_PCL_ )
		INCLUDE_DIRECTORIES(${PCL_INCLUDE_DIRS})
		SET(ADDITIONAL_LIBRARIES ${ADDITIONAL_LIBRARIES} ${PCL_LIBRARIES})
	ELSE()
		MESSAGE(FATAL_ERROR "PCL not found! Much of the libraries can be built without PCL, however, so consider opting not to use it.")
	ENDIF()
	IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
		STRING( FIND ${PCL_COMMON_LIBRARY} "32" APOSITION )
		IF (NOT "${APOSITION}" STREQUAL "-1")
			MESSAGE(WARNING "This is a 64-bit build, and it looks like CMake actually found the 32-bit PCL libraries to link to! This may cause compilation problems..")
		ENDIF()
	ELSE()
		STRING( FIND ${PCL_COMMON_LIBRARY} "64" APOSITION ) 
		IF (NOT "${APOSITION}" STREQUAL "-1")
			MESSAGE(WARNING "This is a 32-bit build, and it looks like CMake actually found the 64-bit PCL libraries to link to! This may cause compilation problems..")
		ENDIF()
	ENDIF()
ENDIF()

find_package(Boost QUIET)
IF(Boost_FOUND)
	OPTION(USE_BOOST "Build Boost-dependent functions." TRUE)
ELSE()
	OPTION(USE_BOOST "Build Boost-dependent functions." FALSE)
ENDIF()

IF(USE_BOOST)
	SET(Boost_USE_STATIC_LIBS OFF) 
	SET(Boost_USE_MULTITHREADED ON)  
	SET(Boost_USE_STATIC_RUNTIME OFF) 
	
	FIND_PACKAGE(Boost COMPONENTS system filesystem program_options date_time REQUIRED)
	
	IF(Boost_FOUND)
		MESSAGE(STATUS "Boost found!")
		
		MESSAGE(STATUS "CMAKE_GENERATOR  = " ${CMAKE_GENERATOR})
		IF(${CMAKE_GENERATOR} STREQUAL "Visual Studio 10 Win64")
			SET(Boost_LIBRARY_DIR "${Boost_INCLUDE_DIR}/lib64-msvc-10.0" CACHE PATH "" FORCE)
		ELSEIF(${CMAKE_GENERATOR} STREQUAL "Visual Studio 10")
			SET(Boost_LIBRARY_DIR "${Boost_INCLUDE_DIR}/lib32-msvc-10.0" CACHE PATH "" FORCE)
		ELSEIF(${CMAKE_GENERATOR} STREQUAL "Visual Studio 11 Win64")
			SET(Boost_LIBRARY_DIR "${Boost_INCLUDE_DIR}/lib64-msvc-11.0" CACHE PATH "" FORCE)
		ELSEIF(${CMAKE_GENERATOR} STREQUAL "Visual Studio 11")
			SET(Boost_LIBRARY_DIR "${Boost_INCLUDE_DIR}/lib32-msvc-11.0" CACHE PATH "" FORCE)
		ELSEIF(${CMAKE_GENERATOR} STREQUAL "Visual Studio 12 Win64")
			SET(Boost_LIBRARY_DIR "${Boost_INCLUDE_DIR}/lib64-msvc-12.0" CACHE PATH "" FORCE)
		ELSEIF(${CMAKE_GENERATOR} STREQUAL "Visual Studio 12")
			SET(Boost_LIBRARY_DIR "${Boost_INCLUDE_DIR}/lib32-msvc-12.0" CACHE PATH "" FORCE)
		ENDIF()
		
		FIND_PACKAGE(Boost COMPONENTS system filesystem program_options date_time REQUIRED)
		
		MESSAGE(STATUS "Boost_INCLUDE_DIR = ${Boost_INCLUDE_DIR}")
		MESSAGE(STATUS "Boost_LIBRARY_DIR = ${Boost_LIBRARY_DIR}")
		
		INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIR})
		link_directories(${Boost_LIBRARY_DIR})
		ADD_DEFINITIONS( -D_USE_BOOST_ )
	ELSE()
		MESSAGE(FATAL_ERROR "Boost not found! It is possible boost is installed, but not for this architecture (e.g. 64-bit libraries may be installed, but not 32-bit, or the version installed may be for the wrong version of Visual Studio.")
	ENDIF()
ELSE()
	IF (NOT BUILD_FOR_ROS)
		MESSAGE(FATAL_ERROR "If not building for ROS, the Boost libraries must be included for timekeeping and other purposes. Please ensure either the <BUILD_FOR_ROS> or <USE_BOOST> flag is set true.")
	ENDIF()
ENDIF()

IF(USE_PCL)
	IF(NOT USE_BOOST)
		MESSAGE(WARNING "Though the user has specified not to include Boost, it may still be included because PCL incorporates Boost anyway.")
	ENDIF()
ENDIF()
	
IF(Boost_FOUND)
	IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
		STRING( FIND ${Boost_SYSTEM_LIBRARY_DEBUG} "lib64" APOSITION )
		IF ("${APOSITION}" STREQUAL "-1")
			MESSAGE(FATAL_ERROR "This is a 64-bit build, and it looks like CMake actually found the 32-bit Boost libraries to link to!")
		ENDIF()
	ELSE()
		STRING( FIND ${Boost_SYSTEM_LIBRARY_DEBUG} "lib64" APOSITION ) 
		IF (NOT "${APOSITION}" STREQUAL "-1")
			MESSAGE(FATAL_ERROR "This is a 32-bit build, and it looks like CMake actually found the 64-bit Boost libraries to link to!")
		ENDIF()
	ENDIF()
ENDIF()

SET(OptrisTestLoc "$ENV{USERPROFILE}/Documents/Optris/SDK")
IF(NOT EXISTS "${OptrisTestLoc}")
	SET(OptrisTestLoc "C:/Users/Public/Documents/Optris/SDK")
	IF(NOT EXISTS "${OptrisTestLoc}")
		OPTION(USE_OPTRIS_SDK "Build with Optris compatability" FALSE)
	ELSE()
		OPTION(USE_OPTRIS_SDK "Build with Optris compatability" TRUE)
	ENDIF()
ELSE()
	OPTION(USE_OPTRIS_SDK "Build with Optris compatability" TRUE)
ENDIF()

IF(BUILD_FOR_ROS)
	ADD_DEFINITIONS( -D_BUILD_FOR_ROS_ )
ENDIF()
	
FIND_PACKAGE( OpenCV HINTS "/usr/local/share/OpenCV" "$ENV{USERPROFILE}/Documents/opencv/build" "C:/Users/Public/Documents/opencv/build" "$ENV{USERPROFILE}/Documents/GitHub/BUILDS/OpenCV")
STRING(REGEX REPLACE "/Release/Release" "/Release" OpenCV_LIB_DIR_OPT "${OpenCV_LIB_DIR_OPT}")
STRING(REGEX REPLACE "/Debug/Debug" "/Debug" OpenCV_LIB_DIR_DBG "${OpenCV_LIB_DIR_DBG}")

IF(OpenCV_FOUND AND ("${OpenCV_DIR}" STREQUAL "${OpenCV_CONFIG_PATH}") AND (NOT("${OpenCV_LIB_DIR_DBG}" STREQUAL "/Debug")) AND (NOT("${OpenCV_LIB_DIR_OPT}" STREQUAL "/Release")) )
	MESSAGE(STATUS "OpenCV Found!")
	STRING(REGEX REPLACE "/lib" "/bin" OpenCV_BIN_DIR_OPT "${OpenCV_LIB_DIR_OPT}")
	STRING(REGEX REPLACE "/lib" "/bin" OpenCV_BIN_DIR_DBG "${OpenCV_LIB_DIR_DBG}")
ELSEIF(EXISTS "${OpenCV_DIR}/")
	MESSAGE(WARNING "OpenCV not entirely found, but directory address <OpenCV_DIR> located and so assuming local build at ${OpenCV_DIR}")
	IF( (NOT EXISTS "${OpenCV_DIR}/lib/Debug/") AND (NOT EXISTS "${OpenCV_DIR}/lib/Release/") )
		MESSAGE(FATAL_ERROR "Provided path for OpenCV does not appear to contain valid debug OR release libraries!")
	ENDIF()
	
	SET(OpenCV_LIB_DIR_OPT "${OpenCV_DIR}/lib/Release" CACHE STRING "..." FORCE)
	SET(OpenCV_LIB_DIR_DBG "${OpenCV_DIR}/lib/Debug" CACHE STRING "..." FORCE)
	SET(OpenCV_3RDPARTY_LIB_DIR_OPT "${OpenCV_DIR}/lib/Release" CACHE STRING "..." FORCE)
	SET(OpenCV_3RDPARTY_LIB_DIR_DBG "${OpenCV_DIR}/lib/Debug" CACHE STRING "..." FORCE)
	
	SET(OPENCV_FOUND TRUE CACHE BOOL "..." FORCE)
	SET(OpenCV_FOUND TRUE CACHE BOOL "..." FORCE)
	
	SET(OpenCV_BIN_DIR_OPT "${OpenCV_DIR}/bin/Release")
	SET(OpenCV_BIN_DIR_DBG "${OpenCV_DIR}/bin/Debug")
	SET(OpenCV_CONFIG_PATH "${OpenCV_DIR}" CACHE STRING "..." FORCE)
ELSE()
	MESSAGE(FATAL_ERROR "OpenCV not found! Please set the <OpenCV_DIR> variable to the OpenCV build directory.")
ENDIF()

IF(OpenCV_FOUND)
	INCLUDE_DIRECTORIES(${OpenCV_INCLUDE_DIRS})
	LIST(APPEND ADDITIONAL_LIBRARIES ${OpenCV_LIBS})
	STRING(REGEX REPLACE "\\." "" OPENCV_VER "${OpenCV_VERSION}")
	IF(OpenCV_VERSION_MAJOR GREATER 2)
		MESSAGE(STATUS "OpenCV Version is 3+, so some functions differ. This should be handled by compiler directives.")
		SET(THERMALVIS_COMPILE_FLAGS "${THERMALVIS_COMPILE_FLAGS} -D_OPENCV_VERSION_3_PLUS_")
	ENDIF()
	IF (OPENCV_GPU_FOUND)
		MESSAGE(STATUS "OpenCV GPU module found, so including it.")
		SET(THERMALVIS_COMPILE_FLAGS "${THERMALVIS_COMPILE_FLAGS} -D_USE_OPENCV_GPU_")
	ENDIF()
ENDIF()

INCLUDE_DIRECTORIES(include)
INCLUDE_DIRECTORIES(${ADDITIONAL_INCLUDES})

FILE(GLOB THERMALVISLIB_HEADERS include/*.h*)
FILE(GLOB THERMALVISLIB_SOURCES src/*.cpp)
ADD_LIBRARY(thermalvis ${THERMALVISLIB_SOURCES} ${THERMALVISLIB_HEADERS} )
TARGET_LINK_LIBRARIES(thermalvis ${ADDITIONAL_LIBRARIES})
SET_TARGET_PROPERTIES(thermalvis PROPERTIES COMPILE_FLAGS ${THERMALVIS_COMPILE_FLAGS})

FILE(GLOB STREAMERLIB_HEADERS include/streamer/*.h*)
FILE(GLOB STREAMERLIB_SOURCES src/streamer/*.cpp)
ADD_LIBRARY(streamer ${STREAMERLIB_SOURCES} ${STREAMERLIB_HEADERS} )
TARGET_LINK_LIBRARIES(streamer ${ADDITIONAL_LIBRARIES})
SET_TARGET_PROPERTIES(streamer PROPERTIES COMPILE_FLAGS ${THERMALVIS_COMPILE_FLAGS})

FILE(GLOB FLOWLIB_HEADERS include/flow/*.h*)
FILE(GLOB FLOWLIB_SOURCES src/flow/*.cpp)
ADD_LIBRARY(flow ${FLOWLIB_SOURCES} ${FLOWLIB_HEADERS} )
TARGET_LINK_LIBRARIES(flow ${ADDITIONAL_LIBRARIES})
SET_TARGET_PROPERTIES(flow PROPERTIES COMPILE_FLAGS ${THERMALVIS_COMPILE_FLAGS})

FILE(GLOB SLAMLIB_HEADERS include/slam/*.h*)
FILE(GLOB SLAMLIB_SOURCES src/slam/*.cpp)
ADD_LIBRARY(slam ${SLAMLIB_SOURCES} ${SLAMLIB_HEADERS} )
TARGET_LINK_LIBRARIES(slam ${ADDITIONAL_LIBRARIES})
SET_TARGET_PROPERTIES(slam PROPERTIES COMPILE_FLAGS ${THERMALVIS_COMPILE_FLAGS})

IF(BUILD_GUI)
	ADD_SUBDIRECTORY(qt)
ENDIF()

IF(BUILD_APPS)
	ADD_SUBDIRECTORY(apps)
ENDIF()

IF(BUILD_FOR_ROS)
	ADD_SUBDIRECTORY(nodes)
ENDIF()
